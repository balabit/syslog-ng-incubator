/*
 * Copyright (c) 2014 BalaBit IT Ltd, Budapest, Hungary
 * Copyright (c) 2016 Yilin Li <liyilin1214@gmail.com>
 *
 * This program is free software; you can redistribute it and/or modify it
 * under the terms of the GNU General Public License version 2 as published
 * by the Free Software Foundation, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 *
 * As an additional exemption you are allowed to compile & link against the
 * OpenSSL libraries as published by the OpenSSL project. See the file
 * COPYING for details.
 *
 */

%code requires {

#include "websocket-parser.h"

}

%code {

#include "cfg-parser.h"
#include "cfg-grammar.h"
#include "plugin.h"

}

%name-prefix "websocket_"
%lex-param {CfgLexer *lexer}
%parse-param {CfgLexer *lexer}
%parse-param {LogDriver **instance}
%parse-param {gpointer arg}


/* INCLUDE_DECLS */

%token KW_WEBSOCKET
%token KW_MODE
%token KW_ADDRESS
%token KW_PORT
%token KW_PROTOCOL
%token KW_PATH
%token KW_SSL
%token KW_CERT
%token KW_KEY
%token KW_CACERT
%token KW_ALLOW_SELF_SIGNED
%token KW_TEMPLATE
%%

start
        : LL_CONTEXT_DESTINATION KW_WEBSOCKET
          {
            last_driver = *instance = websocket_dd_new(configuration);
          }
          '(' websocket_dest_options ')'        { YYACCEPT; }
        | LL_CONTEXT_SOURCE KW_WEBSOCKET
          {
            last_driver = *instance = websocket_sd_new(configuration);
          }
          '(' websocket_src_options ')'		{ YYACCEPT; }
        ;


/* DEST optinos */

websocket_dest_options
        : websocket_dest_option websocket_dest_options
        |
        ;

websocket_dest_option
        : KW_MODE '(' string ')'
          {
            websocket_dd_set_mode(last_driver, $3);
            free($3);
          }
        | KW_PORT '(' LL_NUMBER ')'
          {
            CHECK_ERROR($3 > 0 && $3 < 65535, @3, "Port is out of range!\n");
            websocket_dd_set_port(last_driver, $3);
          }
        | KW_ADDRESS '(' string ')'
          {
            websocket_dd_set_address(last_driver, $3);
            free($3);
          }
        | KW_PROTOCOL '(' string ')'
          {
            websocket_dd_set_protocol(last_driver, $3);
            free($3);
          }
        | KW_PATH '(' string ')'
          {
            websocket_dd_set_path(last_driver, $3);
            free($3);
          }
        | KW_SSL '(' websocket_dd_ssl_options ')'
          {
            websocket_dd_set_enable_ssl(last_driver, TRUE);
          }
        | KW_TEMPLATE '(' string ')'
          {
            websocket_dd_set_template(last_driver, $3);
            free($3);
          }
        | dest_driver_option
        | { last_template_options = websocket_dd_get_template_options(last_driver); } template_option
        ;

websocket_dd_ssl_options
        : websocket_dd_ssl_option websocket_dd_ssl_options
        |
        ;

websocket_dd_ssl_option
        : KW_CERT '(' string ')'
          {
            websocket_dd_set_cert(last_driver, $3);
            free($3);
          }
        | KW_KEY '(' string ')'
          {
            websocket_dd_set_key(last_driver, $3);
            free($3);
          }
        | KW_CACERT '(' string ')'
          {
            websocket_dd_set_cacert(last_driver, $3);
            free($3);
          }
        | KW_ALLOW_SELF_SIGNED
          {
            websocket_dd_set_allow_self_signed(last_driver, TRUE);
          }
        ;


/* SRC options */

websocket_src_options
        : websocket_src_option websocket_src_options
        |
        ;

websocket_src_option
        : KW_PORT '(' LL_NUMBER ')'
          {
            CHECK_ERROR($3 > 0 && $3 < 65535, @3, "Port is out of range!\n");
            websocket_sd_set_port(last_driver, $3);
          }
        | KW_ADDRESS '(' string ')'
          {
            websocket_sd_set_address(last_driver, $3);
            free($3);
          }
        | KW_PROTOCOL '(' string ')'
          {
            websocket_sd_set_protocol(last_driver, $3);
            free($3);
          }
        | KW_PATH '(' string ')'
          {
            websocket_sd_set_path(last_driver, $3);
            free($3);
          }
        | KW_SSL '(' websocket_sd_ssl_options ')'
          {
            websocket_sd_set_enable_ssl(last_driver, TRUE);
          }
        ;

websocket_sd_ssl_options
        : websocket_sd_ssl_option websocket_sd_ssl_options
        |
        ;

websocket_sd_ssl_option
        : KW_CERT '(' string ')'
          {
            websocket_sd_set_cert(last_driver, $3);
            free($3);
          }
        | KW_KEY '(' string ')'
          {
            websocket_sd_set_key(last_driver, $3);
            free($3);
          }
        | KW_CACERT '(' string ')'
          {
            websocket_sd_set_cacert(last_driver, $3);
            free($3);
          }
        ;

/* INCLUDE_RULES */

%%
