/*
 * Copyright (c) 2014 BalaBit IT Ltd, Budapest, Hungary
 * Copyright (c) 2014 Laszlo Meszaros <lacienator@gmail.com>
 *
 * This program is free software; you can redistribute it and/or modify it
 * under the terms of the GNU General Public License version 2 as published
 * by the Free Software Foundation, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 *
 * As an additional exemption you are allowed to compile & link against the
 * OpenSSL libraries as published by the OpenSSL project. See the file
 * COPYING for details.
 *
 */

%code requires {

#include "zmq-parser.h"

}

%code {

#include "cfg-parser.h"
#include "cfg-grammar.h"
#include "plugin.h"

}

%name-prefix "zmq_"
%lex-param {CfgLexer *lexer}
%parse-param {CfgLexer *lexer}
%parse-param {LogDriver **instance}
%parse-param {gpointer arg}


/* INCLUDE_DECLS */

%token KW_ZMQ
%token KW_ADDRESS
%token KW_PORT
%token KW_SOCKET_TYPE
%token KW_TEMPLATE
%%

start
        : LL_CONTEXT_DESTINATION KW_ZMQ
          {
            last_driver = *instance = zmq_dd_new(configuration);
          }
          '(' zmq_dest_options ')'		{ YYACCEPT; }
        | LL_CONTEXT_SOURCE KW_ZMQ
          {
            last_driver = *instance = zmq_sd_new(configuration);
          }
          '(' zmq_src_options ')'		{ YYACCEPT; }
        |
        ;

zmq_dest_options
        : zmq_dest_option zmq_dest_options
        |
        ;

zmq_dest_option
        : KW_PORT '(' LL_NUMBER ')'
          {
            CHECK_ERROR($3 > 0 && $3 < 65535, @3, "Port is out of range!\n");
            zmq_dd_set_port(last_driver, $3);
          }
        | KW_SOCKET_TYPE '(' string ')'
          {
            CHECK_ERROR(zmq_dd_set_socket_type(last_driver, $3), @3,
                        "Unknown zmq socket type: %s", $3);
            free($3);
          }

        | KW_TEMPLATE '(' string ')'
          {
            zmq_dd_set_template(last_driver, $3);
            free($3);
          }
        | dest_driver_option
        | { last_template_options = zmq_dd_get_template_options(last_driver); } template_option
        ;

zmq_src_options
        : zmq_src_option zmq_src_options
        |
        ;

zmq_src_option
        : KW_ADDRESS '(' string ')'
          {
            zmq_sd_set_address(last_driver, $3);
            free($3);
          }
        | KW_PORT '(' LL_NUMBER ')'
          {
            CHECK_ERROR($3 > 0 && $3 < 65535, @3, "Port is out of range!\n");
            zmq_sd_set_port(last_driver, $3);
          }
        ;



/* INCLUDE_RULES */

%%
